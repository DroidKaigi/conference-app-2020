import dependencies.Dep
import dependencies.Packages
import dependencies.Versions

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion Versions.androidCompileSdkVersion
    defaultConfig {
        applicationId Packages.name
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode Versions.androidVersionCode
        versionName Versions.androidVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release.initWith(debug)

        def releaseKeystore = file("release.keystore")

        if (isReleaseBuild) {
            assert releaseKeystore.exists()
            assert System.getenv("RELEASE_KEYSTORE_STORE_PASSWORD")
            assert System.getenv("RELEASE_KEYSTORE_KEY_ALIAS")
            assert System.getenv("RELEASE_KEYSTORE_KEY_PASSWORD")
        }

        if (releaseKeystore.exists()) {
            release {
                storeFile releaseKeystore
                storePassword System.getenv("RELEASE_KEYSTORE_STORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("RELEASE_KEYSTORE_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix Packages.debugNameSuffix
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
        exclude 'META-INF/proguard/*.pro'
    }
    dynamicFeatures = [
            ':feature:staff',
            ':feature:preference',
            ':feature:contributor'
    ]
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }
}

dependencies {
    implementation project(':feature:session')
    implementation project(':feature:system')
    implementation project(':feature:sponsor')
    implementation project(':feature:announcement')
    implementation project(':feature:about')
    implementation project(':feature:floormap')
    implementation project(':feature:session_survey')

    implementation project(':data:repository')
    implementation project(':data:db')
    implementation project(':data:firestore')
    implementation project(':data:api')
    implementation project(':data:device')

    implementation project(':corecomponent:androidcomponent')
    implementation project(':ext:log')

    implementation Dep.Kotlin.stdlibJvm
    implementation Dep.AndroidX.appCompat
    implementation Dep.AndroidX.coreKtx
    implementation Dep.AndroidX.constraint
    implementation Dep.AndroidX.activityKtx
    implementation Dep.Firebase.firestoreKtx
    implementation Dep.Firebase.crashlytics
    implementation Dep.Firebase.analytics

    implementation Dep.Dagger.core
    implementation Dep.Dagger.androidSupport
    implementation Dep.Dagger.android
    kapt Dep.Dagger.compiler
    kapt Dep.Dagger.androidProcessor
    compileOnly Dep.Dagger.assistedInjectAnnotations
    kapt Dep.Dagger.assistedInjectProcessor
    implementation Dep.Groupie.groupie

    testImplementation Dep.Test.junit
    androidTestImplementation Dep.Test.testRunner
    androidTestImplementation Dep.Test.testRules
    androidTestImplementation Dep.Test.espressoCore
    androidTestImplementation Dep.Test.testCoreKtx
    androidTestImplementation Dep.Test.androidJunit4Ktx
    androidTestUtil Dep.Test.orchestrator

    Dep.Hyperion.hyperionPlugins.forEach{
        debugImplementation it
    }
    debugImplementation Dep.Stetho.stetho
    debugImplementation Dep.LeakCanary.leakCanary
}

apply plugin: 'com.google.gms.google-services'
